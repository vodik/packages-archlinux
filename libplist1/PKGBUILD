# $Id: PKGBUILD 183073 2013-04-17 10:22:07Z tomegun $
# Maintainer : Tom Gundersen <teg@jklm.no>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libplist1
_pkgname=libplist
pkgver=1.10
pkgrel=1
pkgdesc="A library to handle Apple Property List format whereas it's binary or XML"
url="http://libimobiledevice.org/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('glib2' 'libxml2' 'python2')
makedepends=('cmake' 'swig')
source=(http://www.libimobiledevice.org/downloads/$_pkgname-$pkgver.tar.bz2)
md5sums=('fe642d0c8602d70c408994555c330dd1')

build() {
  mkdir build
  cd build
  cmake ../$_pkgname-$pkgver \
      -DPYTHON_EXECUTABLE=/usr/bin/python2 \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  rm -fr $pkgdir/usr/{include,lib/pkgconfig,lib/python2.7}
  rm -f $pkgdir/usr/bin/plistutil
  rm -f $pkgdir/usr/lib/libplist{,++}.so
}
